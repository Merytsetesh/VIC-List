100 DIMR$(4)
110 Y$="{HOME}{CRSR-DOWN}{CRSR-DOWN}{CRSR-DOWN}{CRSR-DOWN}{CRSR-DOWN}{CRSR-DOWN}{CRSR-DOWN}{CRSR-DOWN}{CRSR-DOWN}{CRSR-DOWN}{CRSR-DOWN}{CRSR-DOWN}{CRSR-DOWN}{CRSR-DOWN}{CRSR-DOWN}{CRSR-DOWN}{CRSR-DOWN}{CRSR-DOWN}{CRSR-DOWN}{CRSR-DOWN}{CRSR-DOWN}{CRSR-DOWN}":X$="{CRSR-RIGHT}{CRSR-RIGHT}{CRSR-RIGHT}{CRSR-RIGHT}{CRSR-RIGHT}{CRSR-RIGHT}{CRSR-RIGHT}{CRSR-RIGHT}{CRSR-RIGHT}{CRSR-RIGHT}{CRSR-RIGHT}{CRSR-RIGHT}{CRSR-RIGHT}{CRSR-RIGHT}{CRSR-RIGHT}{CRSR-RIGHT}{CRSR-RIGHT}{CRSR-RIGHT}{CRSR-RIGHT}{CRSR-RIGHT}{CRSR-RIGHT}{CRSR-RIGHT}"
120 POKE36879,8:PRINTCHR$(14):SC=7680
130 SR=INT(RND(1)*11+10)
140 DE=INT(RND(1)*11+10):WE=75+INT(RND(1)*25)
150 TS=0:RO=0:FA=100:WO=100:D=1:X=3:Y=15
160 FORI=0TO4:READR$(I):NEXT
170 DATAO93D7L5ED5R1O4D7L4D2L4U2L4U9R3U4L3U6
180 DATAD1OR5E4O3D991L4U9D9L8U9E4O6
190 DATAO3D5RE5U1O4R4D7E4O92L93U994
200 DATAO6D7U7R6D8L2D5R2D8L4E4O4U91R6D4U4L6U1E4OR2L2U6
210 DATAO93D9L9R6D9L1D3L1E4O3U993
220 ONROGOTO240,250,260,270
230 MX=10:MY=3:GOTO280
240 MX=4:MY=3:GOTO280
250 MX=3:MY=10:GOTO280
260 MX=10:MY=18:GOTO280
270 MX=2:MY=18:GOTO280
280 M=0:IFRND(1)>.5THENM=INT(RND(1)*3+1):MS=INT(RND(1)*10)+5
290 GOSUB1300:V=0:F=0:GOTO330
300 GETA$:V=VAL(A$):IFV=0THEN600
310 FA=FA-INT((V/200)*(100-WO)+(V/200)*WE+.5)
320 GOSUB1200:IFM=1ANDRND(1)>.2THENF=1
330 SW=0:FORI=1TOV:PRINTLEFT$(Y$,Y+1);LEFT$(X$,X)"  {CRSR-DOWN}{CRSR-LEFT}{CRSR-LEFT}  "
340 X1=X+(D=3)-(D=1):Y1=Y+(D=0)-(D=2)
350 IF PEEK(SC+Y1*22+X1)<>32ORPEEK(SC+Y1*22+X1+1)<>32THENI=V:GOTO500
360 IF PEEK(SC+Y1*22+X1+22)<>32ORPEEK(SC+Y1*22+X1+23)<>32THENI=V:GOTO500
370 X=X1:Y=Y1:IFX>0ANDX<11ANDY>0ANDY<20THEN500
380 RO=RO+(X=0)-(X=11)+((Y=20)-(Y=0))*2
390 IFX=0THENX=10
400 IFX=11THENX=1
410 IFY=0THENY=19
420 IFY=20THENY=1
430 GOTO220
500 PRINTLEFT$(Y$,Y+1);LEFT$(X$,X)"{RED}";
510 OND+1GOTO520,530,540,550
520 PRINT"{CBM-D} {CRSR-DOWN}{CRSR-LEFT}{CRSR-LEFT}{CBM-K}{CBM-K}{CRSR-UP}{CRSR-LEFT}";:GOTO560
530 PRINT"{RVS-ON}{CBM-I}{RVS-OFF}{CBM-F}{CRSR-DOWN}{CRSR-LEFT}{CRSR-LEFT}{RVS-ON}{CBM-I}{RVS-OFF} {CRSR-LEFT}";:GOTO560
540 PRINT"{RVS-ON}{CBM-K}{CBM-K}{CRSR-DOWN}{CRSR-LEFT}{CRSR-LEFT}{RVS-OFF} {CBM-V}{CRSR-LEFT}{CRSR-LEFT}";:GOTO560
550 PRINT" {CBM-I}{CRSR-DOWN}{CRSR-LEFT}{CRSR-LEFT}{CBM-C}{CBM-I}{CRSR-UP}{CRSR-LEFT}{CRSR-LEFT}";
560 IFV>0THENNEXTI
600 IFA$<>"L"ANDA$<>"R"THEN660
610 IFFA<100THENFA=FA+1:GOSUB1200
620 SW=0:D=D+(A$="L")-(A$="R"):IFD<0THEND=3
630 IFD>3THEND=0
640 A$=""
650 V=0:GOTO500
660 IFA$<>"T"THEN800
670 SW=3:FA=FA-2
680 PRINTLEFT$(Y$,Y+1);LEFT$(X$,X+1);"{YEL}";:IFD=0THENPRINT"{CBM-G}":GOTO720
690 IFD=1THENPRINT"{CRSR-DOWN}{CBM-T}":GOTO720
700 IFD=2THENPRINT"{CRSR-DOWN}{CRSR-LEFT}{CBM-M}":GOTO720
710 PRINT"{CRSR-LEFT}{CBM-@}"
720 X1=X:Y1=Y:X1=X1+((D=3)-(D=1))*2:Y1=Y1+((D=0)-(D=2))*2
730 F=1
800 IFSW>0THENSW=SW-1:IFSW=0THENGOSUB1200:A$="":GOTO500
810 IFM=0THEN1100
820 IFMS>=SRANDRND(1)>.7THENF=1
830 IFF=0THEN920
840 PRINTLEFT$(Y$,MY+1);LEFT$(X$,MX)"  {CRSR-DOWN}{CRSR-LEFT}{CRSR-LEFT}  ";:IFSM>0ANDMX>=XTHENPRINT"{CRSR-UP}{CRSR-UP}{CRSR-LEFT}{CRSR-LEFT}{CRSR-LEFT} "
850 SM=0:DY=MY+(MY>Y)-(MY<Y):IFPEEK(SC+DY*22+MX)<>32ORPEEK(SC+DY*22+MX+1)<>32THEN880
860 IFPEEK(SC+DY*22+MX+22)<>32ORPEEK(SC+DY*22+MX+23)<>32THEN880
870 MY=DY
880 DY=MX+(MX>X+2)-(MX<X-1):IFABS(DY-X)<=1THENDY=DY+(DY>1)*2
890 IFPEEK(SC+MY*22+DY)<>32ORPEEK(SC+MY*22+DY+1)<>32THEN920
900 IFPEEK(SC+MY*22+DY+22)<>32ORPEEK(SC+MY*22+DY+23)<>32THEN920
910 MX=DY
920 PRINTLEFT$(Y$,MY+1);LEFT$(X$,MX)"{BLU}{RVS-ON}{CBM-B}{RVS-OFF}{CBM-F}{CRSR-DOWN}{CRSR-LEFT}{CRSR-LEFT}{CBM-C}";:IFSW=0THEN970
930 IF ABS(X1-MX)>1 OR ABS(Y1-MY)>1 THEN 970
940 SW=1:S=SR/4:S=S+(S*DE)/100:MS=MS-INT(S+.5):IFMS>0THEN970
950 PRINTLEFT$(Y$,MY+1);LEFT$(X$,MX)"  {CRSR-DOWN}{CRSR-LEFT}{CRSR-LEFT} "
960 TS=TS+1:M=0:GOTO1100
970 IFSM>0ORRND(1)>.5ORF=0THEN1040
980 X2=MX:Y2=MY
990 PRINTLEFT$(Y$,MY+1);LEFT$(X$,MX);:IFMX<XTHENPRINT"{CRSR-DOWN}{CRSR-RIGHT}{YEL}{CBM-T}";:Y2=Y2+1:X2=X2+1:GOTO1030
1000 IFMX=0THEN1100
1010 IFPEEK(SC+MY*22+MX-1)=160THEN1100
1020 PRINT"{CRSR-LEFT}{CRSR-UP}{YEL}{CBM-@}";:X2=X2-1
1030 SM=4
1040 IFSM=0THEN1100
1050 SM=SM-1
1060 IFABS(X2-X)>1ORABS(Y2-Y)>1THEN1100
1070 SW=1:S=MS:S=S+(S*2*M)/100:WO=WO-INT(S+.5)
1100 IFWO>0ANDFA>0THEN300
1110 FORI=1TO1000:NEXT:PRINT"{CLR}{CRSR-DOWN}{CRSR-DOWN}{CRSR-DOWN}{YEL}THOU ART":PRINT"    SLAIN":M=0
1120 GOSUB1200:PRINT"{CRSR-UP}{CRSR-UP}";:POKE198,0:END
1130 IFD1=3THENPRINT"{CRSR-LEFT}";
1200 PRINTLEFT$(Y$,2);LEFT$(X$,14)"{CYN}FATIGUE{YEL}{CRSR-DOWN}{CRSR-LEFT}{CRSR-LEFT}{CRSR-LEFT}{CRSR-LEFT}{CRSR-LEFT}{CRSR-LEFT}{CRSR-LEFT}"FA"{CRSR-LEFT}% "
1210 PRINTTAB(14)"{CYN}WOUNDS{CRSR-DOWN}{CRSR-LEFT}{CRSR-LEFT}{CRSR-LEFT}{CRSR-LEFT}{CRSR-LEFT}{CRSR-LEFT}{YEL}"WO"{CRSR-LEFT}% "
1220 PRINTTAB(14)"{CRSR-DOWN}{CYN}WGT{CRSR-DOWN}{CRSR-LEFT}{CRSR-LEFT}{CRSR-LEFT}{YEL}"WE"{CRSR-LEFT}LBS{CRSR-DOWN}"
1230 PRINTTAB(14)"{CYN}TOTAL{CRSR-DOWN}{CRSR-LEFT}{CRSR-LEFT}{CRSR-LEFT}{CRSR-LEFT}{CRSR-LEFT}SLAIN{YEL}{CRSR-DOWN}{CRSR-LEFT}{CRSR-LEFT}{CRSR-LEFT}{CRSR-LEFT}{CRSR-LEFT}"TS
1240 PRINTLEFT$(Y$,17);LEFT$(X$,14)"{PUR}{CBM-@}{CBM-@}{CBM-@}{CBM-@}{CBM-@}{CBM-@}{CBM-@}{CRSR-DOWN}{CRSR-LEFT}{CRSR-LEFT}{CRSR-LEFT}{CRSR-LEFT}{CRSR-LEFT}{CRSR-LEFT}{CRSR-LEFT}{CRSR-LEFT}{CBM-M}       {CBM-G}{CRSR-DOWN}{CRSR-LEFT}{CRSR-LEFT}{CRSR-LEFT}{CRSR-LEFT}{CRSR-LEFT}{CRSR-LEFT}{CRSR-LEFT}{CRSR-LEFT}{CRSR-LEFT}";
1250 PRINT"{CBM-M}       {CBM-G}{CRSR-DOWN}{CRSR-LEFT}{CRSR-LEFT}{CRSR-LEFT}{CRSR-LEFT}{CRSR-LEFT}{CRSR-LEFT}{CRSR-LEFT}{CRSR-LEFT}{CRSR-LEFT}{CBM-M}       {CBM-G}{CRSR-DOWN}{CRSR-LEFT}{CRSR-LEFT}{CRSR-LEFT}{CRSR-LEFT}{CRSR-LEFT}{CRSR-LEFT}{CRSR-LEFT}{CRSR-LEFT}{CRSR-LEFT}{CBM-M}       {CBM-G}";
1260 PRINT"{CRSR-DOWN}{CRSR-LEFT}{CRSR-LEFT}{CRSR-LEFT}{CRSR-LEFT}{CRSR-LEFT}{CRSR-LEFT}{CRSR-LEFT}{CRSR-LEFT}{CBM-T}{CBM-T}{CBM-T}{CBM-T}{CBM-T}{CBM-T}{CBM-T}{CRSR-UP}{CRSR-UP}{CRSR-UP}{CRSR-UP}{CRSR-LEFT}{CRSR-LEFT}{CRSR-LEFT}{CRSR-LEFT}{CRSR-LEFT}{CRSR-LEFT}";
1270 IF M>0 THEN PRINT"{CRSR-DOWN}{CRSR-LEFT}MONSTER{CRSR-DOWN}{CRSR-LEFT}{CRSR-LEFT}{CRSR-LEFT}{CRSR-LEFT}{CRSR-LEFT}{CRSR-LEFT}{CRSR-LEFT}APPEARS"
1280 RETURN
1300 PRINT"{CLR}{WHI}";:D1=1: FORI=1TOLEN(R$(RO))
1310 S$=MID$(R$(RO),I,1):IFVAL(S$)>0THENFORJ=1TOVAL(S$):GOSUB1400:NEXT
1320 IFS$="U"THEND1=0
1330 IFS$="L"THEND1=3
1340 IFS$="D"THEND1=2
1350 IFS$="R"THEND1=1
1360 IFS$="E"THENPRINT"{RVS-OFF}";
1370 IFS$="O"THEN PRINT"{RVS-ON}";
1380 NEXT:PRINT"{HOME}":GOSUB1200:RETURN
1400 PRINT" {CRSR-LEFT}";
1410 IFD1=0THENPRINT"{CRSR-UP}";
1420 IFD1=1THENPRINT"{CRSR-RIGHT}";
1430 IFD1=2THENPRINT"{CRSR-DOWN}";
1440 IFD1=3THENPRINT"{CRSR-LEFT}";
1450 RETURN
3705 IFS$="O"THEN PRINT"{RVS-ON}";
65535 REMB@#4097
